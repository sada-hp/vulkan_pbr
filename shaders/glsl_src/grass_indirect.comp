#version 460
#include "ubo.glsl"
#include "constants.glsl"
#include "common.glsl"
#include "noise.glsl"

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

struct TerrainVertex
{
    vec4 Position;
    vec4 UV;
};

struct IndirectCommand
{
    uint vertexCount;
    uint instanceCount;
    uint firstVertex;
    uint firstInstance;
};

layout (constant_id = 2) const uint RingsCount = 0;
layout (constant_id = 3) const float Scale = 1.f;
layout (constant_id = 4) const float MinHeight = 0;
layout (constant_id = 5) const float MaxHeight = 0;

layout (set = 1, binding = 0) uniform sampler2DArray NoiseMap;
layout (std140, set = 1, binding = 1) readonly buffer TerrainBuffer
{
	TerrainVertex at[];
} vertices;
layout (std140, set = 1, binding = 2) writeonly buffer PositionsBuffer
{
	ivec4 at[];
} positions;
layout (std140, set = 1, binding = 3) buffer IndirectBuffer
{
	IndirectCommand at[];
} commands;

bool Cull(vec3 CenterPosition, mat3 Rotation)
{
    vec4 Box[8] = {
        vec4(-1.0, 0.0, -1.0, 1.0),
        vec4(1.0, 0.0, -1.0, 1.0),
        vec4(-1.0, 10.0, -1.0, 1.0),
        vec4(-1.0, 0.0, 1.0, 1.0),
        vec4(1.0, 10.0, -1.0, 1.0),
        vec4(-1.0, 10.0, 1.0, 1.0),
        vec4(1.0, 0.0, 1.0, 1.0),
        vec4(1.0, 10.0, 1.0, 1.0),
    };

    for (int i = 0; i < 8; i++)
    {
        Box[i] = vec4(CenterPosition + Rotation * vec2(5.0, 10.0).xyx * Box[i].xyz, 1.0);
    }

    for (int i = 0; i < 6; i++)
    {
        if (dot(ubo.FrustumPlanes[i], Box[0]) < 0.0
        && dot(ubo.FrustumPlanes[i], Box[1]) < 0.0
        && dot(ubo.FrustumPlanes[i], Box[2]) < 0.0
        && dot(ubo.FrustumPlanes[i], Box[3]) < 0.0
        && dot(ubo.FrustumPlanes[i], Box[4]) < 0.0
        && dot(ubo.FrustumPlanes[i], Box[5]) < 0.0
        && dot(ubo.FrustumPlanes[i], Box[6]) < 0.0
        && dot(ubo.FrustumPlanes[i], Box[7]) < 0.0)
        {
            return false;
        }
    }

    return true;
}

void main()
{
    ivec2 size = ivec2(textureSize(NoiseMap, 0));
    TerrainVertex vertex = vertices.at[int(gl_GlobalInvocationID.x)];
    int Level = int(vertex.Position.w);
    
    commands.at[Level].vertexCount = Level >= 2 || ubo.CameraRadius > Rg + MaxHeight ? 3 : (Level == 1 ? 15 : 27);

    vec2 WorldUV = vec2(0.0);
    if (abs(vertex.Position.y) > abs(vertex.Position.x) && abs(vertex.Position.y) > abs(vertex.Position.z))
    {
        WorldUV = vertex.Position.xz;
    }
    else if (abs(vertex.Position.z) > abs(vertex.Position.x) && abs(vertex.Position.z) > abs(vertex.Position.y))
    {
        WorldUV = vertex.Position.xy;
    }
    else
    {
        WorldUV = vertex.Position.yz;
    }

    vec2 hash = noise2(WorldUV);
    mat3 Rot = mat3(ubo.PlanetMatrix);
    vec3 p = vertex.Position.xyz + Rot * 0.5 * Scale * exp2(Level) * vec3(hash.x, 0.0, hash.y);

    if (Cull(p, Rot))
    {
        uint index = atomicAdd(commands.at[Level].instanceCount, 1);
        int vtx_new = int(commands.at[Level].firstInstance + index);
        positions.at[vtx_new / 4][vtx_new % 4] = int(gl_GlobalInvocationID.x);
    }
}