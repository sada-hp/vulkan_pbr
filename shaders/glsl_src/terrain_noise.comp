#version 460
#include "ubo.glsl"
#include "constants.glsl"
#include "common.glsl"
#include "noise.glsl"

layout(local_size_x = 32, local_size_y = 1, local_size_z = 1) in;

struct Vertex
{
    vec4 Position;
    vec4 UV;
};

layout (set = 1, binding = 0, r32f) uniform writeonly image2DArray outImage;
layout (std140, set = 1, binding = 1) readonly buffer TerrainVertex{
	Vertex at[];
} vertices;

layout (constant_id = 2) const uint VertexCount = 0;
layout (constant_id = 3) const float Scale = 0;
layout (constant_id = 4) const float MinHeight = 0;
layout (constant_id = 5) const float MaxHeight = 0;
layout (constant_id = 6) const uint Seed = 0;

float sample_perlin_sphere(vec3 Normal, float freq)
{
    return saturate(perlin(Normal.xz, freq) * perlin(Normal.xy, freq) * perlin(Normal.yz, freq));
}

void main()
{
    NOISE_SEED = Seed;

    if (NOISE_SEED != 0 && gl_GlobalInvocationID.x < VertexCount)
    {
        Vertex vertex = vertices.at[gl_GlobalInvocationID.x];

        int Level = int(vertex.Position.y);
#if 1
        dmat3 Orientation = GetTerrainOrientation();
        dvec3 Center = RoundToIncrement(ubo.CameraPositionFP64.xyz, Scale * exp2(vertex.Position.y));
#else
        dmat3 Orientation;
        Orientation[0] = vec3(1.0, 0.0, 0.0);
        Orientation[1] = normalize(dvec3(0.0, Rg, 0.0));
        Orientation[2] = normalize(cross(Orientation[0], Orientation[1]));
        Orientation[0] = normalize(cross(Orientation[1], Orientation[2]));
        dvec3 Center = dvec3(0.0, Rg, 0.0);
#endif
        vec3 ObjectPosition = vec3(Orientation * vec3(vertex.Position.x, 0.0, vertex.Position.z) + Center);
        vec3 Normal = vec3(normalize(ObjectPosition));

        float perlin1 = sample_perlin_sphere(Normal * vec3(0.5, 1.0, 0.5), 250.0);
        float perlin2 = sample_perlin_sphere(Normal, 350.0);
        float perlin3 = sample_perlin_sphere(Normal * vec3(0.75, 1.0, 0.25), 480.0);
        float perlin4 = sample_perlin_sphere(Normal * vec3(0.5, 1.0, 0.5), 100.0);
        float perlin5 = sample_perlin_sphere(Normal, 1000.0);

        float Height = 1.0 - saturate(0.75 * perlin3 + 0.5 * perlin1 + 0.25 * perlin2);
        Height = saturate(sqrt(saturate(Height - perlin4)) * perlin5);

        imageStore(outImage, ivec3(round(vertex.UV.xy * (imageSize(outImage).xy - 1)), Level), vec4(MinHeight + Height * (MaxHeight - MinHeight)));
    }
}
